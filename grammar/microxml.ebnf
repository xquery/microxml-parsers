document        ::= byteOrderMark? (comment | s)* element (comment | s)*
element         ::= startTag content endTag | emptyElementTag
startTag        ::= '<' name attributeList s* '>'
endTag          ::= '</' name s* '>'
content         ::= (element | comment | dataChar | charRef)*
emptyElementTag ::= '<' name attributeList s* '/>'
attributeList   ::= (s+ attribute)*
attribute       ::= attributeName s* '=' s* attributeValue

<?TOKENS?>

byteOrderMark   ::= #xFEFF
attributeValue  ::= '"' ((attributeValueChar - '"') | charRef)* '"'
                      | "'" ((attributeValueChar - "'") | charRef)* "'"
attributeValueChar ::= char - ('<'|'>'|'&')
attributeName      ::= name - 'xmlns'
comment            ::= '<!--' ((char - '-') | ('-' (char - '-')))* '-->'
charRef            ::= numericCharRef | namedCharRef
numericCharRef     ::= '&#x' charNumber ';'

dataChar      ::= char - ('<'|'&'|'>')
charNumber    ::= [0-9a-fA-F]+
namedCharRef  ::= '&' charName ';'
charName      ::= 'amp' | 'lt' | 'gt' | 'quot' | 'apos'
name          ::= nameStartChar nameChar*
nameStartChar ::= [A-Z] | [a-z] | "_" | [#xC0-#xD6] | [#xD8-#xF6] | [#xF8-#x2FF] | [#x370-#x37D] | [#x37F-#x1FFF] | [#x200C-#x200D] | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF] | ([#xF900-#xEFFFF] - nonCharacterCodePoint)
nameChar ::= nameStartChar | [0-9] | "-" | "." | #xB7 | [#x0300-#x036F] | [#x203F-#x2040]
char ::= s | ([#0-#x10FFFF] - forbiddenCodePoint)
s ::= #x9 | #xA | #x20
forbiddenCodePoint ::= controlCodePoint | surrogateCodePoint |
nonCharacterCodePoint
controlCodePoint ::= [#x0-#1F] | [#7F-#9F]
surrogateCodePoint ::= [#xD800-#xDFFF]
nonCharacterCodePoint ::= [#xFDD0-#xFDEF] | [#xFFFE-#xFFFF] | [#x1FFFE-#x1FFFF]
                             | [#x2FFFE-#x2FFFF] | [#x3FFFE-#x3FFFF] | [#x4FFFE-#x4FFFF]
                             | [#x5FFFE-#x5FFFF] | [#x6FFFE-#x6FFFF] | [#x7FFFE-#x7FFFF]
                             | [#x8FFFE-#x8FFFF] | [#x9FFFE-#x9FFFF] | [#xAFFFE-#xAFFFF]
                             | [#xBFFFE-#xBFFFF] | [#xCFFFE-#xCFFFF] | [#xDFFFE-#xDFFFF]
                             | [#xEFFFE-#xEFFFF] | [#xFFFFE-#xFFFFF] | [#x10FFFE-#x10FFFF]
