document ::= byteOrderMark? ( comment | s )* element ( comment | s )* eof
element  ::= '<' name attributeList s? ( '>' content endTag | '/>' )
endTag   ::= '</' name s? '>'
content  ::= ( element | comment | dataChar | charRef )*
attributeList
         ::= ( s attribute )*
attribute
         ::= attributeName s? '=' s? attributeValue

<?TOKENS?>

byteOrderMark
         ::= #xFEFF
attributeValue
         ::= '"' ( ( attributeValueChar - '"' ) | charRef )* '"'
           | "'" ( ( attributeValueChar - "'" ) | charRef )* "'"
attributeValueChar
         ::= char - ( '<' | '>' | '&' )
attributeName
         ::= name - 'xmlns'
comment  ::= '<!--' ( ( char - '-' ) | '-' ( char - '-' ) )* '-->'
charRef  ::= numericCharRef
           | namedCharRef
numericCharRef
         ::= '&#x' charNumber ';'
dataChar ::= char - ( '<' | '&' | '>' )
charNumber
         ::= [0-9a-fA-F]+
namedCharRef
         ::= '&' charName ';'
charName ::= 'amp'
           | 'lt'
           | 'gt'
           | 'quot'
           | 'apos'
name     ::= nameStartChar nameChar*
nameStartChar
         ::= [A-Z]
           | [a-z]
           | '_'
           | [#x00C0-#x00D6]
           | [#x00D8-#x00F6]
           | [#x00F8-#x02FF]
           | [#x0370-#x037D]
           | [#x037F-#x1FFF]
           | [#x200C-#x200D]
           | [#x2070-#x218F]
           | [#x2C00-#x2FEF]
           | [#x3001-#xD7FF]
           | ( [#xF900-#xEFFFF] - nonCharacterCodePoint )
nameChar ::= nameStartChar
           | [0-9]
           | '-'
           | '.'
           | #x00B7
           | [#x0300-#x036F]
           | [#x203F-#x2040]
char     ::= space
           | ( [#0-#x10FFFF] - forbiddenCodePoint )
space    ::= #x0009
           | #x000A
           | #x0020
s        ::= space+
forbiddenCodePoint
         ::= controlCodePoint
           | surrogateCodePoint
           | nonCharacterCodePoint
controlCodePoint
         ::= [#x0000-#x001F]
           | [#x007F-#x009F]
surrogateCodePoint
         ::= [#xD800-#xDFFF]
nonCharacterCodePoint
         ::= [#xFDD0-#xFDEF]
           | [#xFFFE-#xFFFF]
           | [#x1FFFE-#x1FFFF]
           | [#x2FFFE-#x2FFFF]
           | [#x3FFFE-#x3FFFF]
           | [#x4FFFE-#x4FFFF]
           | [#x5FFFE-#x5FFFF]
           | [#x6FFFE-#x6FFFF]
           | [#x7FFFE-#x7FFFF]
           | [#x8FFFE-#x8FFFF]
           | [#x9FFFE-#x9FFFF]
           | [#xAFFFE-#xAFFFF]
           | [#xBFFFE-#xBFFFF]
           | [#xCFFFE-#xCFFFF]
           | [#xDFFFE-#xDFFFF]
           | [#xEFFFE-#xEFFFF]
           | [#xFFFFE-#xFFFFF]
           | [#x10FFFE-#x10FFFF]
eof      ::= $
